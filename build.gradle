plugins {
    id 'org.springframework.boot' version '2.4.0'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id "com.diffplug.spotless" version "5.1.1"
    id "io.freefair.lombok" version "5.1.1"

    id 'java'
    id 'maven-publish'
    id 'idea'
}

group = 'pipelite'
version = '1.0.48'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

bootJar {
    enabled = false
}

jar {
    from sourceSets.main.delombokTask
    enabled = true
}

task sourceJar(type: Jar) {
    from sourceSets.main.delombokTask
    archiveClassifier = 'sources'
}

java {
    withSourcesJar()
}

spotless {
    java {
        licenseHeader('''/*
 * Copyright 2020 EMBL - European Bioinformatics Institute
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this
 * file except in compliance with the License. You may obtain a copy of the License at
 * http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software distributed under the
 * License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
 * CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 */
''')
        importOrder 'java', 'javax', 'org', 'com', '', 'uk'
        removeUnusedImports()
        googleJavaFormat()
    }
}

lombok {
    config['lombok.anyConstructor.addConstructorProperties'] = 'true'
}

repositories {
    mavenLocal()
    maven {
        url "https://gitlab.ebi.ac.uk/api/v4/groups/enasequence/-/packages/maven"

        credentials(HttpHeaderCredentials) {
            name = "Private-Token"
            value = gitlab_private_token
        }
        authentication {
            header(HttpHeaderAuthentication)
        }

    }
    mavenCentral()
    maven { url "http://maven.imagej.net/content/repositories/public/" }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.retry:spring-retry:1.2.5.RELEASE'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'

    implementation 'org.springdoc:springdoc-openapi-ui:1.3.0'
    implementation group: 'com.google.guava', name: 'guava', version: '29.0-jre'
    implementation group: 'com.google.flogger', name: 'flogger', version: '0.5.1'
    implementation group: 'com.google.flogger', name: 'flogger-slf4j-backend', version: '0.5.1'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.11.2'
    implementation group: 'org.apache.sshd', name: 'sshd-core', version: '2.5.1'
    implementation group: 'org.apache.sshd', name: 'sshd-sftp', version: '2.5.1'
    implementation group: 'org.apache.commons', name: 'commons-exec', version: '1.3'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.9'
    implementation group: 'org.hsqldb', name: 'hsqldb', version: '2.5.1'
    implementation 'tech.tablesaw:tablesaw-core:0.38.1'
    implementation 'tech.tablesaw:tablesaw-jsplot:0.38.1'

    implementation('com.cronutils:cron-utils:9.1.1')
    implementation 'com.thedeanda:lorem:2.1'

    compileOnly 'org.projectlombok:lombok:1.18.6'
    annotationProcessor 'org.projectlombok:lombok:1.18.6'

    implementation group: 'com.amazonaws', name: 'aws-java-sdk-batch', version: '1.11.929'

    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.16.1'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.5.0'

    implementation(group: 'com.oracle', name: 'ojdbc8', version: '12.2.0.1.0')
    implementation(group: 'com.oracle', name: 'xdb', version: '12.2.0.1.0')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            // Project specific maven repository in Gitlab.
            url "https://gitlab.ebi.ac.uk/api/v4/projects/2756/packages/maven"
            // Developer token in Gitlab.
            credentials(HttpHeaderCredentials) {
                name = "Private-Token"
                value = project.ext.properties.gitlab_private_token
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}

test {
    systemProperty 'spring.profiles.active', 'test,hsql'
    useJUnitPlatform()
    outputs.upToDateWhen { false }
}

task testOracle(type: Test) {
    systemProperty 'spring.profiles.active', 'test,oracle'
    useJUnitPlatform()
    outputs.upToDateWhen { false }
    group = 'verification'
}
